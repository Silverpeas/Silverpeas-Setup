/*
    Copyright (C) 2000 - 2020 Silverpeas

    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU Affero General Public License as
    published by the Free Software Foundation, either version 3 of the
    License, or (at your option) any later version.

    As a special exception to the terms and conditions of version 3.0 of
    the GPL, you may redistribute this Program in connection with Free/Libre
    Open Source Software ("FLOSS") applications as described in Silverpeas's
    FLOSS exception.  You should have recieved a copy of the text describing
    the FLOSS exception, and it is also available here:
    "http://www.silverpeas.org/docs/core/legal/floss_exception.html"

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU Affero General Public License for more details.

    You should have received a copy of the GNU Affero General Public License
    along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */
import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
  repositories {
    maven {
      url 'https://www.silverpeas.org/nexus/content/groups/silverpeas'
    }
    jcenter()
    mavenLocal()
  }
}

plugins {
  id 'java-gradle-plugin'
  id 'org.ajoberstar.grgit' version '4.0.2' apply true
  id 'idea'
  id 'maven-publish'
  id 'groovy'
  id "net.linguica.maven-settings" version "0.5"
}

description = 'The Gradle plugin to set up Silverpeas. It is used both to install or to upgrade Silverpeas.'
group = 'org.silverpeas'
version = '6.2.3-SNAPSHOT'


/* cache management
configurations {
  all {
    resolutionStrategy {
      cacheDynamicVersionsFor 0, 'seconds'
      cacheChangingModulesFor 0, 'seconds'
    }
  }
}*/

project.configure(project) {
  if (project.version.endsWith('-SNAPSHOT')) {
    project.extensions.snapshot = true
  } else {
    project.extensions.snapshot = false
  }
}

repositories {
  maven {
    name 'silverpeas'
    url 'https://www.silverpeas.org/nexus/content/groups/silverpeas'
  }
  maven {
    name 'gradle-plugins'
    url 'https://plugins.gradle.org/m2/'
  }
  mavenLocal()
}

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
  withSourcesJar()
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
    }
  }

  repositories {
    maven {
      if (project.snapshot) {
        name 'silverpeas-snapshots'
        url 'https://www.silverpeas.org/nexus/content/repositories/snapshots/'
      } else {
        name 'silverpeas'
        url 'https://www.silverpeas.org/nexus/content/repositories/releases/'
      }
    }
  }
}


dependencies {
  api gradleApi()
  api 'org.apache.commons:commons-lang3:3.10'
  api 'commons-io:commons-io:2.7'
  api 'org.apache.commons:commons-dbcp2:2.7.0'
  api 'commons-codec:commons-codec:1.14'
  api 'javax.jcr:jcr:2.0'
  api 'org.apache.jackrabbit:jackrabbit-core:2.18.4'
  runtimeOnly 'org.eclipse.jetty:jetty-jndi:9.4.29.v20200521'
  runtimeOnly 'org.eclipse.jetty:jetty-util:9.4.29.v20200521'
  runtimeOnly 'com.h2database:h2:1.4.200'
  runtimeOnly 'org.postgresql:postgresql:42.2.12'
  runtimeOnly 'net.sourceforge.jtds:jtds:1.3.1'
  testImplementation gradleTestKit()
  testImplementation 'junit:junit:4.13'
  testImplementation('org.spockframework:spock-core:1.3-groovy-2.4') {
    exclude module: 'groovy-all'
  }
}

ext {
  revision = grgit.head().abbreviatedId
}

jar {
  manifest {
    attributes(
        "Build-Version": project.version,
        "Git-Commit": grgit.head().abbreviatedId)
  }
}


task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

processTestResources {
  filter(ReplaceTokens, tokens:[buildDir: buildDir.path.replace('\\', '/')], beginToken: '${', endToken: '}')
}

/*
    Copyright (C) 2000 - 2019 Silverpeas

    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU Affero General Public License as
    published by the Free Software Foundation, either version 3 of the
    License, or (at your option) any later version.

    As a special exception to the terms and conditions of version 3.0 of
    the GPL, you may redistribute this Program in connection with Free/Libre
    Open Source Software ("FLOSS") applications as described in Silverpeas's
    FLOSS exception.  You should have recieved a copy of the text describing
    the FLOSS exception, and it is also available here:
    "http://www.silverpeas.org/docs/core/legal/floss_exception.html"

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU Affero General Public License for more details.

    You should have received a copy of the GNU Affero General Public License
    along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */
import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
  dependencies {
    classpath 'org.hibernate.build.gradle:gradle-maven-publish-auth:2.0.1'
  }
  repositories {
    maven {
      url 'http://www.silverpeas.org/nexus/content/groups/silverpeas'
    }
    jcenter()
    mavenLocal()
  }
}

plugins {
  id 'java-gradle-plugin'
  id 'org.ajoberstar.grgit' version '3.0.0' apply true
  id 'idea'
  id 'maven'
  id 'maven-publish'
  id 'groovy'
}

apply plugin: 'maven-publish-auth'

description = 'The Gradle plugin to set up Silverpeas. It is used both to install or to upgrade Silverpeas.'
group = 'org.silverpeas'
version = '6.1-SNAPSHOT'


/* cache management
configurations {
  all {
    resolutionStrategy {
      cacheDynamicVersionsFor 0, 'seconds'
      cacheChangingModulesFor 0, 'seconds'
    }
  }
}*/

project.configure(project) {
  if (project.version.endsWith('-SNAPSHOT')) {
    project.extensions.snapshot = true
  } else {
    project.extensions.snapshot = false
  }
}

repositories {
  maven {
    name 'silverpeas'
    url 'http://www.silverpeas.org/nexus/content/groups/silverpeas'
  }
  maven {
    name 'gradle-plugins'
    url 'https://plugins.gradle.org/m2/'
  }
  mavenLocal()
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact sourceJar {
        classifier 'sources'
      }
    }
  }
  repositories {
    maven {
      if (project.snapshot) {
        name 'silverpeas-snapshots'
        url 'http://www.silverpeas.org/nexus/content/repositories/snapshots/'
      } else {
        name 'silverpeas'
        url 'http://www.silverpeas.org/nexus/content/repositories/releases/'
      }
    }
  }
}


dependencies {
  compile gradleApi()
  compile 'org.apache.commons:commons-lang3:3.6'
  compile 'commons-io:commons-io:2.4'
  compile 'org.apache.commons:commons-dbcp2:2.2.0'
  compile 'commons-codec:commons-codec:1.10'
  compile 'javax.jcr:jcr:2.0'
  compile 'org.apache.jackrabbit:jackrabbit-core:2.18.2'
  runtime 'org.eclipse.jetty:jetty-jndi:9.4.8.v20171121'
  runtime 'org.eclipse.jetty:jetty-util:9.4.8.v20171121'
  runtime 'com.h2database:h2:1.4.196'
  runtime 'org.postgresql:postgresql:42.1.4'
  runtime 'net.sourceforge.jtds:jtds:1.3.1'
  testCompile gradleTestKit()
  testCompile 'junit:junit:4.12'
  testImplementation('org.spockframework:spock-core:1.1-groovy-2.4') {
    exclude module: 'groovy-all'
  }
}

ext {
  revision = grgit.head().abbreviatedId
}

jar {
  manifest {
    attributes(
        "Build-Version": project.version,
        "Git-Commit": grgit.head().abbreviatedId)
  }
}


task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

processTestResources {
  filter(ReplaceTokens, tokens:[buildDir: buildDir.path.replace('\\', '/')], beginToken: '${', endToken: '}')
}
